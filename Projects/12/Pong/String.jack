// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {

    field Array str;
    field int length, max_length;  // The "real" and max length of array

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        let str = Array.new(Math.max(maxLength, 1));
        let max_length = maxLength;
        let length = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let str[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        var Array new_str;
        var int i;

        // If length = max_length, reallocate a larger array to store the current string (before appending)
        if (length = max_length) {
            let new_str = Array.new(max_length + 5);  // 5 is a design choice
            // Copy old data to new array allocated
            while (i < max_length) {
                let new_str[i] = str[i];
                let i = i + 1;
            }
            // Recycle old memory
            // do str.dispose();
            let str = new_str;
            let max_length = max_length + 5;
        }
        let str[length] = c;
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        let length = length - 1;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int pos, abs_val;
        var int current_char_scan_code, current_digit;
        var int sign;

        let pos = 0;
        let abs_val = 0;
        let sign = 1;

        // If the first char is "-"
        if (str[pos] = 45) {
            let sign = -1;
            let pos = pos + 1;
        }

        while (pos < length) {
            let current_char_scan_code = str[pos];
            // If scan code not between 48 and 57, i.e. current char is not a digit between 0 and 9, return
            if ((current_char_scan_code < 48) | (current_char_scan_code > 57)) {
                return abs_val * sign;
            }

            let current_digit = current_char_scan_code - 48;  // Convert from char (scan code) to int
            let abs_val = abs_val * 10 + current_digit;

            let pos = pos + 1;
        }

        return abs_val * sign;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var String str_to_copy;
        var Array str_to_copy_array;
        var int i;
        var boolean sign;  // false means negative

        let sign = true;
        if (val < 0) {
            let sign = false;
            let val = Math.abs(val);
        }

        let str_to_copy = String.int2String(val);
        let length = str_to_copy.get_len();
        let str_to_copy_array = str_to_copy.get_str();
        // If val < 0
        if (~(sign)) {
            let str = Array.new(6);  // The max number (in abs value) representable is -32768 which has 6 characters
            let str[0] = 45;  // Scan code of the minus sign
            let i = 0;
            while (i < length) {
                let str[i + 1] = str_to_copy_array[i];
                let i = i + 1;
            }
            let length = length + 1;
        }
        else {let str = str_to_copy_array;}
        return;
    }

    /** Helper function which converts an non-negative int to string */
    function String int2String(int val) {
        var int last_digit, last_scan_code, temp;
        var String str_res;

        if (val < 0) {
            do Output.printString("Cannot convert a negative number to string directly ! Call Math.abs first. ");
            do Sys.halt();
        }

        let temp = val / 10;
        let last_digit = val - (temp * 10);  // val mod 10
        let last_scan_code = last_digit + 48;  // Convert from int to char (scan code)
        if (val < 10) {
            let str_res = String.new(6);  // -32768 has 6 digits
            return str_res.appendChar(last_scan_code);
        }
        else {
            let str_res = String.int2String(temp);
            return str_res.appendChar(last_scan_code);
        }
    }

    /** Returns the new line character. */
    function char newLine() {return 128;}

    /** Returns the backspace character. */
    function char backSpace() {return 129;}

    /** Returns the double quote (") character. */
    function char doubleQuote() {return 34;}

    method Array get_str() {return str;}

    method int get_len() {return length;}
}
